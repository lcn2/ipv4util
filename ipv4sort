#!/usr/bin/perl -T
#
# ipv4sort - extract IPV4 addresses from files in sorted order
#
# @(#) $Revision: 1.1 $
# @(#) $Id: ipv4sort,v 1.1 2000/06/21 08:30:00 chongo Exp chongo $
# @(#) $Source: /usr/local/src/cmd/ipv4util/RCS/ipv4sort,v $
#
# usage:
#	ipv4sort [-u] [file ...]
#
# This utility will extract IP addresses from files (or STDIN if no args).
# print them, in order, to STDOUT
#
# Copyright (c) 2000 by Landon Curt Noll.  All Rights Reserved.
#
# Permission to use, copy, modify, and distribute this software and
# its documentation for any purpose and without fee is hereby granted,
# provided that the above copyright, this permission notice and text
# this comment, and the disclaimer below appear in all of the following:
#
#       supporting documentation
#       source copies
#       source works derived from this source
#       binaries derived from this source or from derived source
#
# LANDON CURT NOLL DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE,
# INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO
# EVENT SHALL LANDON CURT NOLL BE LIABLE FOR ANY SPECIAL, INDIRECT OR
# CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF
# USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.
#
# chongo <was here> /\oo/\
#
# Share and enjoy!

# requirements
#
use strict;
use Getopt::Std;
use vars qw($opt_u);

# ip address pattern
#
my $ip_exp = "[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}";

# Main function
#
MAIN:
{
    my @ip_set;		# ip addresses found

    # parse args
    #
    if (!getopts('u')) {
	die "usage: $0 [-u] [file ...]\n";
    }

    # extract IP address from STDIN
    #
    undef $/;
    while (<>) {
	my @raw_data = grep { $_ =~ /^$ip_exp$/o } split(/[^0-9.]+/, $_);
	push(@ip_set, @raw_data), "\n" if ($#raw_data >= 0);
    }

    # do nothing if no addresses found
    #
    exit(0) unless @ip_set;

    # remove non-unique if -u was given
    #
    if ($opt_u) {

	my %seen = {};	# IP addrs that we have seen

	@ip_set = grep { ! $seen{$_}++ } @ip_set;
    }

    # sort extract IP address
    #
    @ip_set = map { $_->[0] }
	      sort {
		$a->[1] <=> $b->[1] ||
		$a->[2] <=> $b->[2] ||
		$a->[3] <=> $b->[3] ||
		$a->[4] <=> $b->[4]
	      }
	      map { [ $_, split /\./ ] } @ip_set;

    # print the resules
    #
    print join("\n", @ip_set), "\n";
}
